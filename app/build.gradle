plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-kapt'
  id "io.github.reactivecircus.app-versioning" version "1.2.0"
}

android {
  signingConfigs {
    release {
      Properties properties = new Properties()
      properties.load(project.rootProject.file("local.properties").newDataInputStream())

      storeFile project.rootProject.file("plum_android.keystore")
      storePassword properties.getProperty("keystore.password")
      keyAlias properties.getProperty("key.alias")
      keyPassword properties.getProperty("key.password")
    }
  }
  compileSdk androidConfigs.compileSdk

  defaultConfig {
    applicationId "com.lofigroup.seeyau"
    minSdk androidConfigs.minSdk
    targetSdk androidConfigs.targetSdk

    versionCode 1
    versionName "v0.0.1"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    signingConfig signingConfigs.release

    javaCompileOptions {
      annotationProcessorOptions {
        arguments += [
            "room.schemaLocation":"$projectDir/schemas".toString(),
            "room.incremental":"true",
            "room.expandProjection":"true"]
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    debug {
      applicationIdSuffix ".debug"
    }
  }
  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  buildFeatures {
    compose = true
  }
  composeOptions {
    kotlinCompilerExtensionVersion = versions.composeCompiler
  }
}

import io.github.reactivecircus.appversioning.SemVer
appVersioning {
  overrideVersionCode { gitTag, providers, variantInfo ->
    if (gitTag == null) {
      1
    } else {
      def semVer = SemVer.fromGitTag(gitTag)
      semVer.major * 1_000_000 + semVer.minor * 1000 + semVer.patch
    }
  }

  overrideVersionName { gitTag, providers, variantInfo ->
    if (gitTag == null) {
      "v0.0.1"
    } else {
      gitTag.toString()
    }
  }
}

dependencies {
  implementation project(":common:ui")
  
  implementation project(":domain:navigator")
  implementation project(":domain:auth")
  implementation project(":domain:profile")
  implementation project(":domain:chat")
  implementation project(":domain:settings")
  implementation project(":domain:base")

  implementation project(":data:navigator")
  implementation project(":data:auth")
  implementation project(":data:profile")
  implementation project(":data:chat")
  implementation project(':data:base')
  implementation project(":data:settings")

  implementation project(":features:navigator-screen")
  implementation project(":features:nearby-service")
  implementation project(":features:splash-screen")
  implementation project(":features:profile-screen")
  implementation project(":features:chat-list-screen")
  implementation project(":features:data-sync-service")
  implementation project(":features:chat-screen")
  implementation project(":features:auth-screen-flow")

  implementation appCompat
  implementation coreKtx

  coreLibraryDesugaring desugaringDep

  testImplementation testDeps.core
  testImplementation testDeps.truth
  androidTestImplementation testDeps.android
  androidTestImplementation testDeps.espresso

  implementation composeDeps.runtime
  implementation composeDeps.ui
  debugImplementation composeDeps.uiTooling
  implementation composeDeps.uiToolingPreview
  implementation composeDeps.foundation
  implementation composeDeps.foundationLayout
  implementation composeDeps.material
  implementation composeDeps.materialIcons
  implementation composeDeps.materialIconsExtended
  implementation composeDeps.activity
  implementation composeDeps.navigation
  debugImplementation composeDeps.poolingContainer

  implementation daggerDeps.core
  kapt daggerDeps.annotationProcessor

  implementation timberDep

  implementation roomDeps.runtime
  implementation roomDeps.ktx
  kapt roomDeps.annotationProcessor
  androidTestImplementation roomDeps.runtime

  implementation coroutinesDeps.core
  testImplementation coroutinesDeps.test
  androidTestImplementation coroutinesDeps.test

  implementation retrofitDeps.core
  implementation retrofitDeps.moshiConverter

  implementation okHttp.core
  implementation okHttp.loggingInterceptor
}